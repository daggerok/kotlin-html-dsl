buildscript {
  ext {
    javaVersion = "1.8"
    kotlinVersion = "1.2.41"
    // publication
    vcsUrl = "https://github.com/daggerok/kotlin-html-dsl.git"
  }
}

plugins {
  id "idea"
  id "java"
  id "maven"
  id "signing"
  id "eclipse"
  id "maven-publish"
  id "com.jfrog.bintray" version "1.8.0"
  id "org.jetbrains.kotlin.jvm" version "1.2.41"
  id "org.jetbrains.kotlin.plugin.noarg" version "1.2.41"
  id "org.jetbrains.kotlin.plugin.allopen" version "1.2.41"
}

/* apply plugin: "base" */

description = "Kotlin HTML DSL: The smallest kotlin extension library you have ever seen!"
group = "com.github.daggerok"
version = "1.0.DOM"
sourceCompatibility = targetCompatibility = "$javaVersion"

defaultTasks "clean", "build", "install", "publishToMavenLocal", "publish"

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

/* apply plugin: "java" */

compileJava {
  options.compilerArgs += ["-Xlint:unchecked"]
}

/* apply plugin: "kotlin" */

compileKotlin {
  incremental = true
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "$javaVersion"
  }
}

compileTestKotlin {
  incremental = true
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "$javaVersion"
  }
}

dependencies {
  //compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
  compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
  testCompile("org.jetbrains.kotlin:kotlin-test:$kotlinVersion")
  //compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.5")
  testCompile("org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion")
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.7"
  distributionType = "ALL"
}

/* apply plugin: "idea" */

idea {
  module {
    downloadSources = true
  }
}

/* apply plugin: "eclipse" */

eclipse {
  classpath {
    downloadSources = true
  }
}

/* apply plugin: "maven-publish" */

task javadocJar(type: Jar) {
  classifier = "javadoc"
  from javadoc
}

javadoc.failOnError = false

task classifierJar(type: Jar) {
  from sourceSets.main.output
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task classifierSourceJar(type: Jar) {
  from sourceSets.main.allJava
}

javadoc.failOnError = false

def pomConfig = {
  scm {
    url "$project.vcsUrl"
  }
  developers {
    developer {
      id "daggerok"
      name "Maksim Kostromin"
      email "daggerok@gmail.com"
    }
  }
  licenses {
    license {
      name "MIT License"
      url "https://raw.githubusercontent.com/daggerok/kotlin-html-dsl/master/LICENSE"
      distribution "repo"
    }
  }
}

publishing {
  publications {
    publication(MavenPublication) {
      from components.java
      artifact javadocJar { classifier = "javadoc" }
      artifact classifierJar { classifier = "1.8" }
      artifact sourceJar { classifier = "sources" }
      artifact classifierSourceJar { classifier = "1.8-sources" }
      pom.withXml {
        def root = asNode()
        root.appendNode("description", project.description)
        root.appendNode("name", project.name)
        root.appendNode("url", project.vcsUrl)
        root.children().last() + pomConfig
      }
    }
  }
  repositories {
    maven {
      url "$buildDir/maven-publish"
    }
  }
}

def getPropOrSystemOrDefault(def propertyName, def systemPropertyName) {
  def defaultValue = System.getenv("$systemPropertyName") ?: "not-present"
  return project.hasProperty("$propertyName") ? project.property("$propertyName") : defaultValue
}

bintray {
  user = getPropOrSystemOrDefault("bintrayUser", "BINTRAY_USER")
  key = getPropOrSystemOrDefault("bintrayApiKey", "BINTRAY_API_KEY")
  publications = ["publication"]
  pkg {
    userOrg = user
    repo = "daggerok"
    name = "$project.name"
    licenses = ["MIT"]
    vcsUrl = project.vcsUrl
    labels = [
        "Kotlin HTML DSL",
        "kotlin-html-dsl",
        "Kotlin to HTML",
        "kotlin-to-html",
        "Convert Kotlin to HTML",
        "convert-kotlin-to-html",
        "Kotlin extensions",
        "Kotlin extension",
        "domain-specific-language",
        "Domain Specific Language",
    ]
    publicDownloadNumbers = true
  }
}

clean.doFirst {
  ["out"].each {
    delete "$projectDir/$it"
  }
}

/* maven cen/tral */
artifacts {
  archives javadocJar, classifierJar, sourceJar, classifierSourceJar
}

// avoid jitpack build fail
// uncomment to publish build to maven central using command: `gradle uploadArchives`
/*

signing {
  sign configurations.archives
}

def ossrhUsername = getPropOrSystemOrDefault("ossrhUsername", "ossrhUsername")
def ossrhPassword = getPropOrSystemOrDefault("ossrhPassword", "ossrhPassword")

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      pom.project {
        name "$project.name"
        packaging "jar"
        // optionally artifactId can be defined here
        description "$project.description"
        url "https://github.com/daggerok/kotlin-html-dsl"
        scm {
          connection "scm:git:git://github.com/daggerok/kotlin-html-dsl.git"
          developerConnection "scm:git:ssh://github.com:daggerok/kotlin-html-dsl.git"
          url "https://github.com/daggerok/kotlin-html-dsl/tree/master"
        }
        licenses {
          license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
          }
        }
        developers {
          developer {
            id "daggerok"
            name "Maksim Kostromin"
            email "daggerok@gmail.com"
          }
        }
      }
    }
  }
}
*/
